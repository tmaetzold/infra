#!/usr/bin/env just --justfile

[private]
default:
    @just --list

# Install ansible dependencies
depends:
    ansible-galaxy install -r requirements.yml

# Ping hosts
ping target="all":
    ansible {{target}} -m ping

# Update all packages
update: depends
    ansible-playbook playbooks/update.yml

# Set env vars (comma-separated)
[group("environment")]
env-set env target="all": depends
    ansible-playbook playbooks/env-set.yml -e "env_input={{env}} target_hosts={{target}}"

# Unset env vars (comma-separated)
[group("environment")]
env-unset vars target="all": depends
    ansible-playbook playbooks/env-unset.yml -e "env_input={{vars}} target_hosts={{target}}"

# Check playbook changes (dry-run)
[group("playbook")]
check playbook="site" *extra_args="": depends
    ansible-playbook playbooks/{{playbook}}.yml --check {{extra_args}}

# Run a playbook (default: site)
[group("playbook")]
play playbook="site" *extra_args="": depends
    ansible-playbook playbooks/{{playbook}}.yml {{extra_args}}
